// Block definition (there may be more than one block per file)
FUNCTION_BLOCK navigation

// Define input variables
VAR_INPUT
    relative_position : REAL;
    orientation : REAL;
END_VAR

// Define output variable
VAR_OUTPUT
    steering_angle : REAL;
END_VAR

// Fuzzify input variable 'relative_position'
FUZZIFY relative_position
    TERM right := (20, 1) (27,1) (41,0); 
    TERM mid_right := trian 27 41 55;
    TERM center := trian 41 55 69;
    TERM mid_left := trian 55 69 83;
    TERM left := (69,0) (83,1) (90,1);
END_FUZZIFY

// Fuzzify input variable 'orientation'
FUZZIFY orientation
    TERM left := (-30,1) (-15,0);
    TERM mid_left := trian -30 -15 0;
    TERM center := trian -15 0 15;
    TERM mid_right := trian 0 15 30;
    TERM right := (15,0) (30,1);
END_FUZZIFY

// Defuzzify output variable 'steering_angle'
DEFUZZIFY steering_angle
    TERM left := (-30,1) (-15,0);
    TERM mid_left := trian -30 -15 0;
    TERM center := trian -15 0 15;
    TERM mid_right := trian 0 15 30;
    TERM right := (15,0) (30,1);
    // Use 'Center Of Gravity' defuzzification method
    METHOD : COG;
    // Default value is 0 (if no rule activates defuzzifier)
    DEFAULT := 0;
END_DEFUZZIFY

RULEBLOCK No1
    // Use 'min' for 'and' (also implicit use 'max'
    // for 'or' to fulfill DeMorgan's Law)
    AND : MIN;
    // Use 'min' activation method
    ACT : MIN;
    // Use 'max' accumulation method
    ACCU : MAX;

    RULE  1 : IF relative_position IS left      AND orientation IS left THEN steering_angle IS right;
    RULE  2 : IF relative_position IS mid_left  AND orientation IS left THEN steering_angle IS right;
    RULE  3 : IF relative_position IS center    AND orientation IS left THEN steering_angle IS mid_right;
    RULE  4 : IF relative_position IS mid_right AND orientation IS left THEN steering_angle IS mid_right;
    RULE  5 : IF relative_position IS right     AND orientation IS left THEN steering_angle IS center;

    RULE  6 : IF relative_position IS left      AND orientation IS mid_left THEN steering_angle IS right;
    RULE  7 : IF relative_position IS mid_left  AND orientation IS mid_left THEN steering_angle IS mid_right;
    RULE  8 : IF relative_position IS center    AND orientation IS mid_left THEN steering_angle IS mid_right;
    RULE  9 : IF relative_position IS mid_right AND orientation IS mid_left THEN steering_angle IS center;
    RULE 10 : IF relative_position IS right     AND orientation IS mid_left THEN steering_angle IS mid_left;
    
    RULE 11 : IF relative_position IS left      AND orientation IS center THEN steering_angle IS mid_right;
    RULE 12 : IF relative_position IS mid_left  AND orientation IS center THEN steering_angle IS mid_right;
    RULE 13 : IF relative_position IS center    AND orientation IS center THEN steering_angle IS center;
    RULE 14 : IF relative_position IS mid_right AND orientation IS center THEN steering_angle IS mid_left;
    RULE 15 : IF relative_position IS right     AND orientation IS center THEN steering_angle IS mid_left;
    
    RULE 16 : IF relative_position IS left      AND orientation IS mid_right THEN steering_angle IS mid_right;
    RULE 17 : IF relative_position IS mid_left  AND orientation IS mid_right THEN steering_angle IS center;
    RULE 18 : IF relative_position IS center    AND orientation IS mid_right THEN steering_angle IS mid_left;
    RULE 19 : IF relative_position IS mid_right AND orientation IS mid_right THEN steering_angle IS mid_left;
    RULE 20 : IF relative_position IS right     AND orientation IS mid_right THEN steering_angle IS left;
    
    RULE 21 : IF relative_position IS left      AND orientation IS mid_right THEN steering_angle IS center;
    RULE 22 : IF relative_position IS mid_left  AND orientation IS mid_right THEN steering_angle IS mid_left;
    RULE 23 : IF relative_position IS center    AND orientation IS mid_right THEN steering_angle IS mid_left;
    RULE 24 : IF relative_position IS mid_right AND orientation IS mid_right THEN steering_angle IS left;
    RULE 25 : IF relative_position IS right     AND orientation IS mid_right THEN steering_angle IS left;
    
END_RULEBLOCK

END_FUNCTION_BLOCK